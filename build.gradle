plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle' version '[6.0.18,6.2)'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'org.spongepowered.mixin' version '0.7.+'
}

version = minecraft_version + "-" + new Date().format("dd.MM.yyyy")
group = mod_group

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: mapping_channel, version: mapping_version

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        configureEach {
            workingDirectory project.file("run/${it.name}")
            property 'forge.logging.markers', 'SCAN' /* REGISTRIES / REGISTRYDUMP */
            property 'forge.logging.console.level', 'debug'

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        client {
            property 'forge.enabledGameTestNamespaces', mod_id
            // Support for mixins of other mods
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
        }

        clientRandom {
            parent runs.client
            // #### will be replaced with a random number | UUID is a randomly generated one
            args '--username', 'Dev####', '--uuid', 'fdb0e318-1ac3-4049-b6f8-d024c0b87a56'
        }

        server {
            property 'forge.enabledGameTestNamespaces', mod_id
            // Support for mixins of other mods
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
        }

        data {
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven { /* GeckoLib */ url "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/" }
    maven { /* JEI */ url "https://maven.blamejared.com" }
    maven { /* Curios */ url "https://maven.theillusivec4.top/" }
    maven { /* AppleSkin */ url "https://maven.ryanliptak.com/" }
    maven { /* Curseforge */ url "https://cursemaven.com" }
}

dependencies {
    minecraft "net.neoforged:forge:${minecraft_version}-${neo_version}"

    // Dependencies
    implementation fg.deobf("software.bernie.geckolib:geckolib-forge-${minecraft_version}:${geckolib_version}")
    implementation "com.eliotlash.mclib:mclib:20"

    // Compatibility
    compileOnly fg.deobf("mezz.jei:jei-${minecraft_version}-common-api:${jei_version}")

    compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:${curios_version}+${minecraft_version}:api")
    runtimeOnly fg.deobf("top.theillusivec4.curios:curios-forge:${curios_version}+${minecraft_version}")

    compileOnly fg.deobf("squeek.appleskin:appleskin-forge:${appleskin_version}:api")

    // Mixin
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    // MixinExtras (we can remove this once we update to a newer version of NeoForge)
    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:0.3.6"))
    implementation group: 'io.github.llamalad7', name: 'mixinextras-common', version: '0.3.6'
    implementation(jarJar("io.github.llamalad7:mixinextras-forge:0.3.6")) {
        jarJar.ranged(it, "[0.3.6,)")
    }
}

mixin {
    config "${mod_id}.mixins.json"
    add sourceSets.main, "${mod_id}.refmap.json"
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version           : minecraft_version,
            minecraft_version_range     : minecraft_version_range,
            neo_version                 : neo_version,
            neo_version_range           : neo_version_range,
            loader_version_range        : loader_version_range,
            geckolib_version_range      : geckolib_version_range,
            mod_id                      : mod_id,
            mod_name                    : mod_name,
            mod_license                 : mod_license,
            mod_version                 : mod_version,
            mod_authors                 : mod_authors,
            pack_format_number          : pack_format_number,
    ]

    duplicatesStrategy = DuplicatesStrategy.WARN

    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                'Specification-Title'     : mod_id,
                'Specification-Vendor'    : mod_authors,
                'Specification-Version'   : '1',
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : mod_authors,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    finalizedBy 'reobfJar'
}

tasks.named('jarJar', Jar).configure {
    jarJar.enable()

    manifest {
        attributes([
                'Specification-Title'     : mod_id,
                'Specification-Vendor'    : mod_authors,
                'Specification-Version'   : '1',
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : mod_authors,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    finalizedBy 'reobfJarJar'
}


tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}